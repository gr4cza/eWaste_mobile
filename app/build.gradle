//file:noinspection DifferentStdlibGradleVersion
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId "hu.bme.ewaste"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "url"
    productFlavors{
        local{
            dimension "url"
            applicationIdSuffix ".local"
            manifestPlaceholders versionSuffix: 'local'
            buildConfigField 'String', 'BASE_URL', '" "' // "<your local ip address:port>"
        }
        cloud{
            dimension "url"
            applicationIdSuffix ".cloud"
            manifestPlaceholders versionSuffix: 'cloud'
            buildConfigField 'String', 'BASE_URL', '"ewaste-backend.herokuapp.com"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        // Enables Jetpack Compose for this module
        compose true
    }
    androidResources {
        noCompress 'tflite'
    }

    namespace 'hu.bme.ewaste'

    composeOptions {
        kotlinCompilerExtensionVersion "1.1.0-beta04"
    }
}

dependencies {
    // AAR libraries
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2-native-mt'

    // MediaPipe dependencies
    implementation 'com.google.flogger:flogger:0.7.1'
    implementation 'com.google.flogger:flogger-system-backend:0.7.1'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'com.google.protobuf:protobuf-java:3.11.4'

    // AutoValue
    def auto_value_version = '1.8.2'
    implementation "com.google.auto.value:auto-value-annotations:$auto_value_version"
    kapt "com.google.auto.value:auto-value:$auto_value_version"

    def camerax_version = '1.1.0-alpha11'
    implementation "androidx.camera:camera-core:$camerax_version"
    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation 'androidx.camera:camera-view:1.0.0-alpha31'

    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:29.0.1')
    // Firebase SDK for Google Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'
    // Ktx extension to firebase tasks
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.2-native-mt'

    // Activity-Ktx
    implementation 'androidx.activity:activity-ktx:1.4.0'

    // Fragment-Ktx
    implementation "androidx.fragment:fragment-ktx:1.4.0"

    // ViewModel
    def lifecycle_version = '2.4.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Access location
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    // Ktor client for http calls
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "ch.qos.logback:logback-classic:1.2.7"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.1"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-compose:2.4.0-beta02"

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.0.5'
    // Animations
    implementation 'androidx.compose.animation:animation:1.0.5'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.0.5'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.0'
    // LiveData
    implementation "androidx.compose.runtime:runtime-livedata:1.0.5"
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.0.5'
    // When using a AppCompat theme
    implementation "com.google.accompanist:accompanist-appcompat-theme:0.16.0"
}